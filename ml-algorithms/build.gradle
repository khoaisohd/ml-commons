/*
 * Copyright OpenSearch Contributors
 * SPDX-License-Identifier: Apache-2.0
 */

import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
    id 'java'
    id 'jacoco'
    id "io.freefair.lombok"
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly group: 'org.opensearch', name: 'opensearch', version: "${opensearch_version}"
    implementation project(':opensearch-ml-common')
    implementation "org.opensearch.client:opensearch-rest-client:${opensearch_version}"
    testImplementation "org.opensearch.test:framework:${opensearch_version}"
    implementation "org.opensearch:common-utils:${common_utils_version}"
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.10.0'
    implementation group: 'org.reflections', name: 'reflections', version: '0.9.12'
    implementation group: 'org.tribuo', name: 'tribuo-clustering-kmeans', version: '4.2.1'
    implementation group: 'org.tribuo', name: 'tribuo-regression-sgd', version: '4.2.1'
    implementation group: 'org.tribuo', name: 'tribuo-anomaly-libsvm', version: '4.2.1'
    implementation group: 'org.tribuo', name: 'tribuo-classification-sgd', version: '4.2.1'
    implementation group: 'commons-io', name: 'commons-io', version: '2.11.0'
    implementation 'software.amazon.randomcutforest:randomcutforest-parkservices:3.0-rc3'
    implementation 'software.amazon.randomcutforest:randomcutforest-core:3.0-rc3'
    implementation group: 'io.protostuff', name: 'protostuff-core', version: '1.8.0'
    implementation group: 'io.protostuff', name: 'protostuff-runtime', version: '1.8.0'
    implementation group: 'io.protostuff', name: 'protostuff-api', version: '1.8.0'
    implementation group: 'io.protostuff', name: 'protostuff-collectionschema', version: '1.8.0'
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.4.0'
    testImplementation group: 'org.mockito', name: 'mockito-inline', version: '4.4.0'
    implementation group: 'com.google.guava', name: 'guava', version: '32.0.1-jre'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.10.1'
    implementation platform("ai.djl:bom:0.25.0")
    implementation group: 'ai.djl.pytorch', name: 'pytorch-model-zoo'
    implementation group: 'ai.djl', name: 'api'
    implementation group: 'ai.djl.huggingface', name: 'tokenizers'

    if (project.hasProperty('includePytorchNativeDependencies')) {
        implementation group: 'ai.djl.pytorch', name: 'pytorch-jni'
        implementation group: 'ai.djl.pytorch', name: "pytorch-native-${project.pytorchFlavor}", classifier: "${project.platformClassifier}"
    }

    implementation("ai.djl.onnxruntime:onnxruntime-engine:0.21.0") {
        exclude group: "com.microsoft.onnxruntime", module: "onnxruntime"
    }
    def os = DefaultNativePlatform.currentOperatingSystem
    //mac doesn't support GPU
    if (os.macOsX) {
        dependencies {
            implementation "com.microsoft.onnxruntime:onnxruntime:1.14.0"
        }
    } else {
        dependencies {
            implementation "com.microsoft.onnxruntime:onnxruntime_gpu:1.14.0"
        }
    }

    implementation platform('software.amazon.awssdk:bom:2.20.19')
    implementation 'software.amazon.awssdk:auth'
    implementation 'software.amazon.awssdk:apache-client'
    implementation 'com.amazonaws:aws-encryption-sdk-java:2.4.1'
    implementation 'com.jayway.jsonpath:json-path:2.8.0'
    implementation group: 'org.json', name: 'json', version: '20230227'

    // OCI SDK to download file from OCI Object Storage
    implementation("com.oracle.oci.sdk:oci-java-sdk-objectstorage:2.38.0") {
        // Exclude org.bouncycastle:bcprov-jdk15on to avoid conflict with bcprov-ext-jdk18on
        // that is provided from com.amazonaws:aws-encryption-sdk-java
        exclude group: 'org.bouncycastle', module: 'bcprov-jdk15on'
        // Exclude jackson and javax.annotation library since they are already provided by
        // opensearch
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-annotations'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
        exclude group: 'com.fasterxml.jackson.module', module: 'jackson-module-jaxb-annotations'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-jdk8'
        exclude group: 'javax.annotation', module: 'javax.annotation-api'
        // Exclude org.javassist:javassist and com.google.guava:guava since they are already
        // provided in ml common plugin
        exclude group: 'org.javassist', module: 'javassist'
        exclude group: 'com.google.guava', module: 'guava'
    }
    implementation("com.fasterxml.jackson.core:jackson-annotations:2.15.2")
    implementation("com.fasterxml.jackson.core:jackson-databind:2.15.2")
}

configurations.all {
    resolutionStrategy.force 'com.google.protobuf:protobuf-java:3.21.9'
}

jacocoTestReport {
    reports {
        xml.getRequired().set(true)
        csv.getRequired().set(false)
        html.getRequired().set(true)
    }

    dependsOn test
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'LINE'
                minimum = 0.65 //TODO: increase coverage to 0.90
            }
            limit {
                counter = 'BRANCH'
                minimum = 0.55 //TODO: increase coverage to 0.85
            }
        }
    }
    dependsOn jacocoTestReport
}
check.dependsOn jacocoTestCoverageVerification
